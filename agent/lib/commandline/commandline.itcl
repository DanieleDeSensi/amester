#
# (C) Copyright IBM Corporation 2011, 2016
#

#--------------------------------------------------------------------
#
# Console window
#
# The console window uses the array command()
# to hold all state.
# command(line) is the command
# command(history) is the array of previous commands
# command(index) points into the history array
# command(count) is the command number
#
# We replicate some of the Tcl history commands because
# they do not appear to work inside starpacks (dqkit)
#
# The console is in the window .cmdline
#
# Cmd uses the singleton pattern. There is only one console window 
# per application.
#
# Version 2.0
# - re-write of Version 1 in [incr tcl]
# - history command only prints in console window, not application stdout.
#
# Version 1.0
# - Initial release
#
#--------------------------------------------------------------------

package provide commandline 2.0

package require Tk
package require Itcl

namespace eval ::commandline:: {

    class Cmd {

	private common _instance {} ;# static instance variable
	
	private variable command
	private variable winname ""
	private variable counter 0 ;# unique name of cmd window
	
	constructor { } {

	    if {$_instance == {}} {
		set _instance $this
		# Command history always holds a blank command at the beginning
		# so that commands are 1..N, like tcl history.
		# There is also a blank command at position N (the current one)
		set command(count) 0
		set command(index) 1
		set command(history) [list "" ""]
		set command(line) ""
		init
	    } else {
		error "Class Cmd (singleton pattern) already instantiated"
	    }
	}
	
	destructor {
	    destroy $winname
	    set _instance {}
	}
	
	# Object Methods
	private method init {}
	private method up {}
	private method down {}
	private method clip {}
	private method process {}

	public method show {}
	public method hide {}
	public method history {}
	public method puts {args}
    }

    namespace export commandline

}

# Up-arrow
body ::commandline::Cmd::up {} {
    incr command(index) -1
    if {$command(index) == 0} {
	#we moved beyond the end of list. move back
	incr command(index)
    } else {
	#only set the command line if we moved the index
	set command(line) [lindex $command(history) $command(index)]
	${winname}.f2.entry icursor end
    }
}

# Down-arrow
body ::commandline::Cmd::down {} {
    incr command(index)
    if {$command(index) == [llength $command(history)]} {
	#we moved beyond the end of list. move back
	incr command(index) -1
    } else {
	#only set the command line if we moved the index
	set command(line) [lindex $command(history) $command(index)]
	${winname}.f2.entry icursor end
    }
}

#
# Trap puts for console. Non-console puts pass thru to Tcl ::puts
#
# Tcl puts is defined: puts ?-nonewline? ?channelID? string
#

# Clip command window to last 1000 lines
body ::commandline::Cmd::clip {} {
    ${winname}.text mark set start {end - 1000 lines}
    ${winname}.text delete 1.0 start
}

# Evaluate a command in global command(line) variable
body ::commandline::Cmd::process {} {
    variable command

    if {[info complete $command(line)]} {

	if {$command(line) eq "!!"} {
	    set command(line) [lindex $command(history) end-1]
	    process
	    return
	}
	
	set command(history) [linsert $command(history) end-1 $command(line)]
	incr command(count)
	if {[llength $command(history)] > 102} {
	    set command(history) [lreplace $command(history) 1 1]
	}
	set command(index) [expr [llength $command(history)] - 1]

	${winname}.text insert end "$command(line)\n" cmd
	set code [catch {uplevel \#0 $command(line)} result]
	if {$result ne ""} {
	    ${winname}.text insert end "$result\n"
	}
	${winname}.text yview moveto 1.0
	clip

	set command(line) {}
    }
}


body ::commandline::Cmd::show {} {
    if {![winfo exists $winname]} {
	init
    }
    if {[winfo exists $winname]} {
	wm state $winname normal
	focus $winname
	raise $winname
    }
}

body ::commandline::Cmd::hide {} {
    if {[winfo exists $winname]} {
	wm iconify $winname
    }
}


body ::commandline::Cmd::init {} {
    #variable command
    set winname ".cmdline[incr counter]"
    toplevel $winname
    wm title $winname "Command line"
    wm withdraw $winname

    frame ${winname}.f1
    text ${winname}.text -wrap word -yscrollcommand [list ${winname}.yscroll set] -width 80 -height 10
    scrollbar ${winname}.yscroll -orient vertical -command [list ${winname}.text yview]
    frame ${winname}.f2
    entry ${winname}.f2.entry -textvariable [scope command(line)] -relief sunken
    label ${winname}.f2.l -text "Enter command:"
    grid ${winname}.f2.l ${winname}.f2.entry -sticky news
    bind ${winname}.f2.entry <Return> [code $this process]
    bind ${winname}.f2.entry <KeyPress-Up> [code $this up]
    bind ${winname}.f2.entry <KeyPress-Down> [code $this down]

    grid ${winname}.text ${winname}.yscroll -sticky news
    grid ${winname}.f2 -sticky news
    grid rowconfigure ${winname} 0 -weight 1
    grid columnconfigure ${winname} 0 -weight 1

    grid columnconfigure ${winname}.f2 1 -weight 1
    
    ${winname}.text tag configure cmd -foreground blue

    wm protocol ${winname} WM_DELETE_WINDOW [code $this hide]

}

body ::commandline::Cmd::history {} {
    variable command
    # set count to first element
    set count [expr $command(count) - [llength $command(history)] + 3]
    # don't show first or last dummy elements
    set l [expr [llength $command(history)] - 1]
    for {set i 1} {$i < $l} {incr i} {
	${winname}.text insert end [format "%5i %s\n" $count [lindex $command(history) $i]]
	incr count
    }
}

body ::commandline::Cmd::puts {args} {

    # defaults
    set err 0
    set nonewline 0
    set stream stdout

    set myargs $args

    if [string match "-nonewline" [lindex $args 0]] {
	set nonewline 1
	set myargs [lreplace $myargs 0 0]
    }
    
    switch -- [llength $myargs] {
	1 {set string [lindex $myargs 0]}
	2 {
	    set stream [lindex $myargs 0]
	    set string [lindex $myargs 1]
	}
	default {set err 1}
    }

    #Copy output to console window, if stdout or stderr
    switch -- $stream {
	stdout {}
	stderr {}
	default {set err 1}
    }

    clip

    #Write console window, if no errors
    if {!$err} {
	if {$nonewline} {
	    ${winname}.text insert end "$string"
	} else {
	    ${winname}.text insert end "$string\n"
	}
    } else {
	# err=1 means do not write to console
    }
    
    # Always pass-thru to ::puts and return its results
    if [catch "::commandline::commandline_puts_original $args" result] {
	::commandline::commandline_puts_original $result
	return -code error $result
    }
    return $result
}

proc ::commandline::commandline {} {
    mycmd show
}

proc ::commandline::commandline_puts {args} {
    eval mycmd puts $args
}

proc ::commandline::history {} {
    mycmd history
}


# Do this only if you want to copy stdout/stderr to console window
rename ::puts ::commandline::commandline_puts_original
interp alias {} ::puts {} ::commandline::commandline_puts

# Do this only to make "history" work in dqkit starpacks
#rename ::history ::commandline_history_original
interp alias {} ::history {} ::commandline::history

# Initialize commandline, but do not show it until run again.
::commandline::Cmd ::commandline::mycmd
